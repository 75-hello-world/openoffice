'*************************************************************************
'
'  Licensed to the Apache Software Foundation (ASF) under one
'  or more contributor license agreements.  See the NOTICE file
'  distributed with this work for additional information
'  regarding copyright ownership.  The ASF licenses this file
'  to you under the Apache License, Version 2.0 (the
'  "License"); you may not use this file except in compliance
'  with the License.  You may obtain a copy of the License at
'  
'    http://www.apache.org/licenses/LICENSE-2.0
'  
'  Unless required by applicable law or agreed to in writing,
'  software distributed under the License is distributed on an
'  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
'  KIND, either express or implied.  See the License for the
'  specific language governing permissions and limitations
'  under the License.
'
'*************************************************************************

REM  *****  BASIC  *****

Option Explicit

Sub ResetRegistration
	' access the global configuration provider
	Dim aConfigProvider As Object
	aConfigProvider = createUnoService( "com.sun.star.configuration.ConfigurationProvider" )
	
	' params for creating config access objects
	Dim aParams(0) As new com.sun.star.beans.PropertyValue
	aParams(0).Name = "nodepath"

	' create an access object for the jobs key
	Dim aRegReq As Object
	aParams(0).Value = "/org.openoffice.Office.Jobs/Jobs/RegistrationRequest"
	aRegReq = aConfigProvider.createInstanceWithArguments( _
		"com.sun.star.configuration.ConfigurationUpdateAccess", _
		aParams() )

	' reset the user time (which means the job will be executed next time)
	aRegReq.setPropertyToDefault( "UserTime" ) ' = "01.01.2001/00:00:00"
	aRegReq.commitChanges

	' create an access object for the Registration key
	Dim aRegistrationSettings As Object
	aParams(0).Value = "/org.openoffice.Office.Common/Help/Registration"
	aRegistrationSettings = aConfigProvider.createInstanceWithArguments( _
		"com.sun.star.configuration.ConfigurationUpdateAccess", _
		aParams() )

	aRegistrationSettings.RequestDialog = 1
	aRegistrationSettings.setPropertyToDefault( "ReminderDate" )
	aRegistrationSettings.commitChanges
End Sub

